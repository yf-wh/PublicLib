apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
// add plugin
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '1.0.6'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 4
        versionName "1.0.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'
    compile files('src/libs/commons-net-3.0.1.jar')
    compile 'com.jcraft:jsch:0.1.54'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'wh.public.lib'
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'wh.public.lib' //自己定义的组名
            artifactId 'library'

            name 'lib'
            description 'A simple test lib.'
            url 'https://github.com/yf-wh/PublicLib'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/yf-wh/PublicLib.git'
                url 'https://github.com/yf-wh/PublicLib'

            }
            developers {
                developer {
                    name 'yf-wh'
                    email 'wuh@yf-space.com'
                }
            }
        }
    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'wh-lib'
        name = 'test-lib'
        vcsUrl = 'https://github.com/yf-wh/PublicLib.git'
        websiteUrl = 'https://github.com/yf-wh/PublicLib'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/yf-wh/PublicLib/issues'
        publicDownloadNumbers = true
        version {
            name = '1.0.6'
            desc = 'my test lib'
            vcsTag = '1.0.6'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}